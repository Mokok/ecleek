grammar org.processus.ecleek.Leek with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate leek "http://www.processus.org/ecleek/Leek"

Script:
	statements+=ToplevelStatement*;

ToplevelStatement:
	Include ';'
	| GlobalDeclaration ';'
	| FunctionDeclaration
	| Statement	
;

Statement:
	{EmptyStatement} ';'
	| LocalDeclaration ';'
	| FunctionCall ';'
	| Iteration
	| If
	| Affectation ';'
	| StatementBlock
;

IterationStatement:
	Statement
	| BreakStatement ';'
	| ContinueStatement ';'
;

FunctionStatement:
	Statement
	| ReturnStatement ';'
;

FunctionStatementBlock:
	'{' {FunctionStatementBlock} statements+=FunctionStatement* '}'
;

BreakStatement:
	{BreakStatement} 'break'
;

ContinueStatement:
	{ContinueStatement} 'continue'
;

StatementBlock:
	'{' {StatementBlock} statements+=Statement* '}'
;

Affectation:
	variable=VariableReference '=' value=Expression
;

If:
	'if' '(' condition=Expression ')' then=Statement (=> 'else' else=Statement)?
;

Iteration:
	While
	| ForIn
	| For
;

While:
	'while' '(' condition=Expression ')' statement=IterationStatement
;

For:
	'for' '(' initializer=ForInitializer ';' condition=Expression ';' increment=Expression ')' statement=IterationStatement
;

ForIn:
	'for' '(' (key=ForInVariableReference ':')? value=ForInVariableReference 'in' array=Expression ')' statement=IterationStatement
;

ForInVariableReference returns VariableReference:
	'var' name=ID
	| variable=[VariableReference]
;

ForInitializer:
	VariableReference
;

FunctionDeclaration:
	'function' name=ID '(' (parameters+=ParameterDeclaration (',' parameters+=ParameterDeclaration)*)? ')' body=FunctionStatementBlock
;

ParameterDeclaration returns VariableReferenceable:
	(byAdress?='@')? name=ID
;

LocalDeclaration:
	'var' variables+=VariableDeclaration (',' variables+=VariableDeclaration)*
;

GlobalDeclaration:
	'global' variables+=VariableDeclaration (',' variables+=VariableDeclaration)*
;

VariableDeclaration:
	name=ID ('=' value=Expression)?
;

Expression:
	Comparison;

Comparison returns Expression:
	Or (({TypedEquals.left=current} '===' | {Equals.left=current} '==' | {TypedDifferent.left=current} '!==' | {Different.left=current} '!=' | {LessOrEquals.left=current} '<=' | {Less.left=current} '<' | {MoreOrEquals.left=current} '>=' | {More.left=current} '>') right=Or)*;

Or returns Expression:
	And (({TypedEquals.left=current} ('or'|'||')) right=And)*;

And returns Expression:
	Addition (({TypedEquals.left=current} ('and'|'&&')) right=Addition)*;

Addition returns Expression:
	Multiplication (({Plus.left=current} '+' | {Minus.left=current} '-') right=Multiplication)*;

Multiplication returns Expression:
	PrimaryExpression (({Multi.left=current} '*' | {Div.left=current} '/') right=PrimaryExpression)*;

PrimaryExpression returns Expression:
	'(' Expression ')'
	| {RealLiteral} value=REAL
	| {IntLiteral} value=INT
	| FunctionCall
	| VariableReference
	| {StringLiteral} value=STRING
	| ArrayLiteral
	| FunctionDeclaration
	| {NullLiteral} 'null'
	| {FalseLiteral} 'false'
	| {TrueLiteral} 'true'
;

ArrayLiteral:
	{ArrayLiteral} '[' (values+=Expression (',' values+=Expression)*)? ']'
;

VariableReferenceable:
	LocalDeclaration
	| GlobalDeclaration
	| ParameterDeclaration
;

VariableReference:
	variable=[VariableReferenceable] ('[' dimensions+=Expression ']')*
;

FunctionCall:
	{FunctionCall} function=[FunctionDeclaration] '(' (args+=Expression (',' args+=Expression)*)? ')'
;

ReturnStatement:
	'return' value=Expression
;

Include:
	'include' '(' importURI=STRING ')'
;

terminal REAL returns ecore::EDouble:
	('0'..'9')* '.' ('0'..'9')+
;
